apply plugin: "java"

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
sourceCompatibility = targetCompatibility = appJavaCompatibility

sourceSets.main.resources.srcDirs = [new File(project(':core').projectDir, "/src/main/assets"),
                                     new File(project(':desktop').projectDir,"/src/main/assets")]

task runDebug(dependsOn: classes, type: JavaExec) {
    main = "com.shatteredpixel.shatteredpixeldungeon.desktop.DesktopLauncher"
    classpath = sourceSets.main.runtimeClasspath

    ignoreExitValue = true
}

task releaseJAR(dependsOn: classes, type: Jar) {
    from sourceSets.main.output
    from { configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) } }

    manifest {
        attributes 'Main-Class': "com.shatteredpixel.shatteredpixeldungeon.desktop.DesktopLauncher"
        attributes 'Specification-Name': appName
        attributes 'Specification-Version': appVersionName
        attributes 'Implementation-Version': appVersionCode
    }
}

dependencies {
    implementation project(':core')

    implementation "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    implementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
}